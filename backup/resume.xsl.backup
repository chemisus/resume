<xsl:stylesheet version="2.0"
                xmlns:xs="http://www.w3.org/2001/XMLSchema"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns="http://www.w3.org/1999/xhtml">
    <xsl:template match="resume">
        <html>
            <head>
                <title>resume</title>
                <link href="resume.css" type="text/css" rel="stylesheet"/>
            </head>
            <body>
                <header>
                    <h1>
                        <xsl:value-of select="contact/name"/>
                    </h1>
                    <xsl:value-of select="contact/email"/>
                </header>
                <article>

                    <section id="experiences">
                        <h1>Experience</h1>
                        <xsl:apply-templates select="experience"/>
                    </section>
                    <section id="educations">
                        <h1>Education</h1>
                        <xsl:apply-templates select="education"/>
                    </section>
                </article>
            </body>
        </html>
    </xsl:template>

    <xsl:template match="experience">
        <section class="experience">
            <div class="header">
                <xsl:apply-templates select="organization"/>
                <xsl:apply-templates select="position"/>
                <xsl:apply-templates select="location"/>
                <xsl:call-template name="daterange">
                    <xsl:with-param name="from" select="from"/>
                    <xsl:with-param name="to" select="to"/>
                </xsl:call-template>
                <xsl:apply-templates select="keywords"/>
            </div>
            <div class="body">
                <xsl:apply-templates select="description"/>
                <xsl:apply-templates select="tasks"/>
                <xsl:apply-templates select="accomplishments"/>
            </div>
        </section>
    </xsl:template>

    <xsl:template name="date">
        <xsl:param name="value"/>
        <xsl:value-of select="$value"/>
        <!--<xsl:value-of select=""/>-->
        <!--<xsl:value-of select="substring($value, 6, 2)"/>&lt;!&ndash;/<xsl:value-of select="substring($value, 9, 2)"/>&ndash;&gt;/<xsl:value-of select="substring($value, 1, 4)"/>-->
    </xsl:template>

    <xsl:template name="daterange">
        <xsl:param name="from"/>
        <xsl:param name="to"/>
        <div>
            <xsl:call-template name="date">
                <xsl:with-param name="value" select="$from"/>
            </xsl:call-template>
            -
            <xsl:choose>
                <xsl:when test="$to">
                    <xsl:call-template name="date">
                        <xsl:with-param name="value" select="$to"/>
                    </xsl:call-template>
                </xsl:when>
                <xsl:otherwise>
                    Current
                </xsl:otherwise>
            </xsl:choose>
        </div>
    </xsl:template>

    <xsl:template match="location">
        <div>
            <xsl:apply-templates select="city"/>,
            <xsl:apply-templates select="state"/>
        </div>
    </xsl:template>

    <xsl:template match="education">
        <div>
            <xsl:apply-templates select="organization"/>
            <xsl:apply-templates select="degree"/>
        </div>
    </xsl:template>

    <xsl:template match="organization">
        <h1>
            <xsl:value-of select="."/>
        </h1>
    </xsl:template>

    <xsl:template match="position">
        <div>
            <xsl:value-of select="."/>
        </div>
    </xsl:template>

    <xsl:template match="degree">
        <div>
            <xsl:value-of select="."/>
        </div>
    </xsl:template>

    <xsl:template match="description">
        <p>
            <xsl:value-of select="."/>
        </p>
    </xsl:template>

    <xsl:template match="tasks">
        <section class="tasks">
            <h1>Tasks</h1>
            <ul>
                <xsl:apply-templates select="item"/>
            </ul>
        </section>
    </xsl:template>

    <xsl:template match="accomplishments">
        <section class="accomplishments">
            <h1>Accomplishments</h1>
            <ul>
                <xsl:apply-templates select="item"/>
            </ul>
        </section>
    </xsl:template>

    <xsl:template match="keywords">
        <section class="keywords">
            <h1>Keywords</h1>
            <ul>
                <xsl:apply-templates select="item"/>
            </ul>
        </section>
    </xsl:template>

    <xsl:template match="item">
        <li class="item">
            <xsl:value-of select="@label"/>
            <ul>
                <xsl:apply-templates select="item"/>
            </ul>
        </li>
    </xsl:template>
</xsl:stylesheet>
